// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   Mux16(a=x[0..15], b[0..15]=false, sel=zx, out=outZX); // Output A if sel is 0, else B
   Not16(in=outZX, out=outXNot16);
   Mux16(a=outZX, b=outXNot16, sel=nx, out=outNX);
   Mux16(a=y[0..15], b[0..15]=false, sel=zy, out=outZY);
   Not16(in=outZY, out=outYNot16);
   Mux16(a=outZY, b=outYNot16, sel=ny, out=outNY);
   Add16(a=outNX, b=outNY, out=outAdd16);
   And16(a=outNX, b=outNY, out=outAnd16);
   Mux16(a=outAnd16, b=outAdd16, sel=f, out=outF);
   Not16(in=outF, out=outXYNot16);
   Mux16(a=outF, b=outXYNot16, sel=no, out[0..7]=outRight, out[8..15]=outLeft, out=out, out[15]=ng);
   Or8Way(in=outLeft, out=orOut1);
   Or8Way(in=outRight, out=orOut2);
   Or(a=orOut1, b=orOut2, out=orOut);
   Not(in=orOut, out=zr);
}